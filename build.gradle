/*
 * This file is part of emuStudio.
 *
 * Copyright (C) 2006-2020  Peter Jakubƒço
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import java.text.SimpleDateFormat

ext.versions = [
  slf4j: '1.7.30'
]

ext.libs = [
  emuLib         : "net.emustudio:emulib:11.5.0",
  cpuTestSuite   : "net.emustudio:cpu-testsuite_11.5:1.1.0",

  javaCupRuntime : "com.github.vbmacher:java-cup-runtime:11b-20160615",
  jcipAnnotations: "net.jcip:jcip-annotations:1.0",

  slf4JApi       : "org.slf4j:slf4j-api:${versions.slf4j}",
  slf4JSimple    : "org.slf4j:slf4j-simple:${versions.slf4j}",
  slf4JNop       : "org.slf4j:slf4j-nop:${versions.slf4j}",
  logback        : "ch.qos.logback:logback-classic:1.2.3",

  args4j         : "args4j:args4j:2.33",
  tomlj          : "com.electronwill.night-config:toml:3.6.2",

  editor         : "com.fifesoft:rsyntaxtextarea:3.1.1",
  editorDialogs  : "com.fifesoft:rstaui:3.1.0",

  junit          : "junit:junit:4.13",
  easyMock       : "org.easymock:easymock:4.2",
  mockito        : "org.mockito:mockito-all:1.9.5"
]

allprojects {
  version = '0.40-SNAPSHOT'

  configurations {
    compilerLib
    memoryLib
    deviceLib
    cpuLib

    implementation.extendsFrom(compilerLib)
    implementation.extendsFrom(memoryLib)
    implementation.extendsFrom(deviceLib)
    implementation.extendsFrom(cpuLib)
  }

  def libClassPath = {
    def libs = configurations.runtimeClasspath.files.collect { "lib/" + it.getName() } +
      configurations.compilerLib.files.collect { "compiler/" + it.getName() } +
      configurations.memoryLib.files.collect { "memory/" + it.getName() } +
      configurations.deviceLib.files.collect { "device/" + it.getName() } +
      configurations.cpuLib.files.collect { "cpu/" + it.getName() }

    return libs.join(' ')
  }

  ext.manifestAttributes = { String mainClass ->
    def baseAttributes = [
      'Class-Path'            : libClassPath(),
      "Implementation-Title"  : project.name,
      "Implementation-Version": project.version,
      'Build-Timestamp'       : new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
      'Build-Jdk'             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
      'Build-OS'              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    ]

    if (mainClass != null && mainClass != '') {
      baseAttributes.put('Main-Class', mainClass)
    }

    return baseAttributes
  }
}

subprojects {
  repositories {
    mavenCentral()
  }
}
