import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java'
  id 'org.xbib.gradle.plugin.jflex' version "1.2.1"
  id "cup.gradle.cup-gradle-plugin" version "1.2"
}

dependencies {
  implementation libs.emuLib
  implementation project.rootProject.project(":plugins:mem:rasp-mem")

  testImplementation libs.junit
  testImplementation libs.easyMock
}

sourceSets.main.java.srcDirs = [
  "${buildDir}/generated-sources/jflex", "${buildDir}/generated-sources/cup", 'src/main/java'
]

jflex {
  generateDir = project.file("${buildDir}/generated-sources/jflex/net/sf/emustudio/rasp/compiler")
}

cup {
  generateDir = "${buildDir}/generated-sources/cup/net/sf/emustudio/rasp/compiler"
  args = [
    "-parser", "ParserImpl", "-symbols", "Symbols", "-interface"
  ]
}

jar {
  archiveVersion = ''
  manifest {
    attributes manifestAttributes('net.sf.emustudio.rasp.compiler.Main')
  }
}

processResources {
  filesMatching("**/*.properties") {
    filter ReplaceTokens, tokens: [
      "project.version": project.version
    ]
  }
}


copy {
  from('src/main/examples')
  into "$buildDir/libs/examples"
}

copy {
  from('src/main/scripts')
  into "$buildDir/libs/scripts"
}
