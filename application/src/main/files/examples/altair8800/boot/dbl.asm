; Disk Boot Loader (DBL)
; Compilable by emuStudio (emuStudio doesn't support ORG without code relocation yet)

;THE ALTAIR 88-DSK ROM MOVES THIS CODE TO 4C00H THEN
; JUMPS TO IT. POSSIBLE ERRORS ARE:
;    C  CRC ON DISK DOESN'T MATCH CALCULATED CRC
;    M  MEMORY ERROR READ/BACK FROM MEMORY DOESN'T MATCH WRITE TO MEMORY (NO RAM, BAD RAM, OR WPROT)
;    O  OVERFLOW OR OUT OF MEM CAUSED BY TRYING TO LOAD BEYOND FF00 OR BAD "LOAD LIMIT BYTES" ON DISK.

ORG 0FF00h

; RELOCATE $E6 BYTES REMAINING OF LOADER TO 4C00 (19456D)
LXI H, 0x4C00
LXI D, 0xFF18
MVI C, 0xE6

lFF08:
LDAX D
MOV M,A
INX D
INX H
DCR C
JNZ lFF08
JMP 0x4C00

nop
nop
nop
nop
nop

lFF18:
DI            ; DISABLE INTERRUPTS
LXI  SP,0x4D62 ; LOAD STACK POINTER
XRA  A        ; ZERO IN A
OUT  08h      ; SELECT DRIVE 0
MVI  A,04h    ; HEAD DOWN CMD
OUT  09h      ; SEND HEAD DOWN
JMP  0x4C19   ; (TTRK0); GO TEST FOR TRACK ZERO

TMVOK:        ; at 0x4C0E
IN 08h        ; GET STATUS
ANI  02h      ; TEST FOR MOVE OK
JNZ  0x4C0E   ; (TMVOK) JMP MOVE NOT OK
MVI  A,02h    ; STEP OUT CMD
OUT  09h      ; SEND STEP OUT

TTRK0:        ; at 0x4C19
IN 08h        ; GET STATUS
ANI  40h      ; TEST FOR TRACK ZERO
JNZ  0x4C0E   ; (TMVOK) JMP NOT TRACK ZERO
LXI  D,0000   ; DE->0000H

NXTTK:        ; at 0x4C23
MVI  B,00     ; B=0

THDDN:        ; at 0x4C25
IN   08h      ; GET STATUS
ANI  04h      ; TEST FOR HEAD DOWN
JNZ  0x4C25   ; (THDDN) JMP NOT HEAD DOWN

;SEEK TO SECTOR IN B
NXTSEC:       ; at 0x4C2C
MVI  A,10h    ; A=10H 16D RETRY COUNT

RETRY:        ; at 0x4C2E
PUSH PSW      ; SAVE RETRY COUNT
PUSH D        ; NXTMEM
PUSH B        ; B=SECTOR
PUSH D        ; NXTMEM
LXI  D,8086h  ; D=80(UNUSED?) E=128=COUNT
LXI  H,0x4CD4 ; (VAR1) M=POINTER TO INBUFFER

TSECT:        ; at 0x4C38
IN   09h      ; GET SECTOR# UNDER HEAD AND STATUS
RAR           ; TEST SECTOR TRUE
JC  0x4C38    ; (TSECT) JMP TSECT IF NOT TRUE
ANI  1Fh      ; AND SECTOR MASK 31D
CMP  B        ; DESIRED SECTOR
JNZ  0x4C38   ; (TSECT) JMP TSECT IF NOT EQUAL

;READ 134 BYTES TWO AT A TIME INTO INBUFFER
TRDOK:        ; at 0x4C44
IN 08h        ; GET STATUS
ORA  A        ; SET FLAGS TEST FOR READ OK
JM   0x4C44   ; (TRDOK) JMP TRDOK IF NOT READ OK
IN   0Ah      ; GET DATA BYTE1
MOV  M,A      ; MOVE BYTE TO INBUFF
INX  H        ; BUMP INPOINTER
DCR  E        ; DECREMENT COUNT
JZ   0x4C5A   ; (MVBYTS) JMP MVBYTS IF DONE
DCR  E        ; DEC COUNT
IN   0Ah      ; GET DATA BYTE2
MOV  M,A      ; MOVE TO INBUFF
INX  H        ; INCREMENT POINTER
JNZ  0x4C44   ; (TRDOK) JMP TRDOK IF COUNT IN E IS NOT ZERO

;MOVE 128 DATA BYTES FROM BUFF TO MEMORY MAKE A CRC
MVBYTS:       ; at 0x4C5A
POP  H        ; HL (M)=0000H FIRST TIME
LXI  D,0x4CD7 ; (VAR3) DE POINTS TO BUFFER + 3
LXI  B,0x80   ; B=0=CRC C=128 COUNT

MVBYT2:       ; at 0x4C61
LDAX D        ; GET BYTE FROM BUFFER
MOV  M,A      ; MOVE TO DESTINATION
CMP  M        ; CHECK FOR MEMORY PROBLEM
JNZ  0x4CC1   ; (MERROR) JMP MERROR IF PROBLEM
ADD  B        ; MAKE A CRC
MOV  B,A      ; SAVE IN B
INX  D        ; INC SRC
INX  H        ; INC DEST
DCR  C        ; DEC COUNT
JNZ  0x4C61   ; (MVBYT2) JMP MVBYT2 UNTIL COUNT = 0

;CHECK CRC
LDAX D        ; DE POINTS TO BUFFER + 131
CPI  0FFh     ; COMPARE THAT BYTE TO 255
JNZ  0x4C78   ; (FFERR) JMP FFERR IF NOT EQUAL
INX  D        ; INC DE POINTS TO BUFF+132
LDAX D        ; GET THAT BYTE
CMP  B        ; COMPARE TO CRC

FFERR:        ; at 0x4C78
POP  B        ; B=SECTOR
XCHG          ; DE->NEXT DEST   HL->BUFF+132
JNZ  0x4CB5   ; (CRCERR) CRC ERROR GO CRCERR

;CHECK FOR OUT OF MEMORY  CHECK DONE LOADING
POP  PSW      ; 0000H FIRST TIME
POP  PSW      ; ACC=16
LHLD 0x4CD5   ; (VAR2) L=BUFF+1 H=BUFF+2
PUSH D        ; SAVE NEXT DEST  0080 FIRST TIME
LXI  D,0FF00h ; DE->BOOTROM(END OF MEMORY)
CALL 0x4CCE   ; (SUB1) COMPARE1 FF00H,HL OUT OF MEMORY (HL GREATER THAN FF00H)
POP  D        ;   NEXT DEST
JC   0x4CBE   ; (OERROR) JMP 'O'UT OF RAM ERROR (HL GREATER THAN FFOOH)
CALL 0x4CCE   ; (SUB1) COMPARE2 DE,HL ARE WE DONE LOADING?
JNC  0x4CAE   ; (EXIT1) IF NO CARRY (HL <= NEXT DEST)WE'RE DONE GO EXECUTE

;CALCULATE NEXT SECTOR AND TRACK
INR  B        ; ADD 2 TO SECTOR#
INR  B        ;
MOV  A,B      ; DESIRED NEXT SECTORA
CPI  0x20     ; TEST IF SECTORA LESS THAN 32
JC   0x4C2C   ; (NXTSEC) IF LESS GO GET IT
MVI  B,01h    ;   IF NOT LESS MAKE DESIRED SECTORB=1
JZ   0x4C2C   ; (NXTSEC) IF SECTORA EQUALED 32 GO GET SECTORB=1

LOOP7:        ; at 0x4CA0
IN   08h      ; ELSE GET STATUS
ANI  02h      ; TEST MOVE OK
JNZ  0x4CA0   ; (LOOP7) UNTIL MOVE IS OK
MVI  A,01h    ; STEP IN CMD
OUT  09h      ; SEND STEP IN CMD
JMP  0x4C23   ; (NXTTK) START LOADING FROM NEXT TRACK SECTORB=0

;LOAD THE EVEN SECTORS ON A TRACK FIRST
;THEN THE ODD SECTORS

;DONE
EXIT1:        ; at 0x4CAE
MVI  A,80h    ;
OUT  08h      ; CLEAR CONTROLLER
JMP  0000h    ; GO

;CRC ERROR RETRY PSW TIMES
CRCERR:       ; at 0x4CB5
POP  D
POP  PSW      ;
DCR  A        ; DECREMENT RETRY COUNT
JNZ  0x4C2E   ; (RETRY) IF NOT 0 GO RETRY

;CLEAR CONTROLLER  SEND TO CONSOLE ERROR MESSAGE
MVI  A,43h    ; ELSE LOAD A WITH CHAR 'C'RC ERROR
DB  01h

;4CBD     LXI  B,4F3E  ;GARBAGE - SKIP TRICK BD-BF
OERROR:       ; at 0x4CBE
MVI  A,4Fh    ; LOAD A WITH CHAR 'O'VERFLOW
DB  01h

;4CC0   LXI  B,4D3E  ;GARBAGE - SKIP TRICK C0-C2
MERROR:       ; at 0x4CC1
MVI  A,4D     ; LOAD A WITH CHAR 'M'EMORY ERROR
MOV  B,A      ; SAVE CHAR
MVI  A,80h    ; CLEAR CONTROLLER
OUT  08h      ; SEND CLEAR
MOV  A,B      ; GET CHAR

CONOUT:       ; at 0x4CC9
OUT  11h      ; SEND ERROR CHAR TO CONSOLE (originally 1h)
JMP  0x4CC9   ; (CONOUT) OVER AND OVER AND OVER...

;TEST FOR OVERFLOW, TEST LOAD LIMIT, COMPARE HL,DE
SUB1:         ; at 0x4CCE
MOV  A,D      ;
CMP  H        ;
RNZ           ;
MOV  A,E      ;
CMP  L        ;
RET

;BEGINNING OF IN BUFFER
VAR1:         ; at 0x4CD4
DB 84h        ; ADD H

VAR2:         ; at 0x4CD5
DB 0          ; NOP
DB 4Ch        ; MOV C,H

VAR3:         ; at 0x4CD7
DB 24h        ; INR H
DB 0D6h, 56h  ; SUI $56
DB 16h, 0     ; MVI D,0
NOP           ; DATA TRUNCATED
